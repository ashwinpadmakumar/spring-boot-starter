plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
}

/*
    What is bootRun?
    The bootRun task is a Gradle task provided by the Spring Boot Gradle plugin. It is used to run the Spring Boot application.
*/
bootRun {
    if (project.hasProperty('jvmArgs')) {
        jvmArgs = (project.jvmArgs.split(/\s+/) as List)
    }
    if (project.hasProperty('args')) {
        args = (project.args.split(/\s+/) as List)
    }
}

jar {
    manifest {
        attributes "Main-Class": "com.workspace.service.ServiceApplication"
    }
}

group = 'com.workspace'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

/*
    This is how we can define a custom gradle task.
    How to run this?
    ./gradlew customTask
*/
tasks.register('customTask') {
    doFirst {
        println 'Starting custom task...'
    }
    doLast {
        println 'Finished custom task.'
    }
}

/*
    Runs when we run ./gradlew clean
*/
tasks.clean.doLast {
    def logDir = file("${projectDir}/logs")
    if (logDir.exists()) {
        println "Deleting log folder: ${logDir}"
        logDir.deleteDir()
    } else {
        println "Log folder not found: ${logDir}"
    }
}

